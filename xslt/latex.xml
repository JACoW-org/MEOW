<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">
<xsl:output omit-xml-declaration="yes" indent="no" encoding="UTF-8" method="text" media-type="text/plain"/>

<xsl:template match="reference">
<xsl:variable name="lowercase" select="'abcdefghijklmnopqrstuvwxyz'" />
<xsl:variable name="uppercase" select="'ABCDEFGHIJKLMNOPQRSTUVWXYZ'" />
<xsl:text>%\cite{</xsl:text>
<xsl:value-of select="contribution/authors-list/author/last-name"/>
<xsl:text>:</xsl:text>
<xsl:value-of select="translate(conference/code, $uppercase, $lowercase)"/>
<xsl:text>-</xsl:text>
<xsl:value-of select="translate(contribution/paper-code, $uppercase, $lowercase)"></xsl:value-of>
<xsl:text>}
</xsl:text>
<xsl:text>\bibitem{</xsl:text>
<xsl:value-of select="contribution/authors-list/author/last-name"/>
<xsl:text>:</xsl:text>
<xsl:value-of select="translate(conference/code, $uppercase, $lowercase)"/>
<xsl:text>-</xsl:text>
<xsl:value-of select="translate(contribution/paper-code, $uppercase, $lowercase)"></xsl:value-of>
<xsl:text>}
    </xsl:text>
<xsl:variable name="set" select="contribution/authors-list/author"/>
<xsl:variable name="count" select="count($set)"/>
<xsl:choose>
<xsl:when test="$count > 3">
<xsl:value-of select="substring(contribution/authors-list/author/first-name, 1, 1)"/>
<xsl:text>. </xsl:text>
<xsl:value-of select="contribution/authors-list/author/last-name"/>
<xsl:text> \emph{et al.}</xsl:text>
</xsl:when>
<xsl:otherwise>
<xsl:for-each select="$set">
<xsl:variable name="index" select="position()"/>
<xsl:if test="$index > 1">
<xsl:choose>
<xsl:when test="$index = $count"><xsl:text> and </xsl:text></xsl:when>
<xsl:otherwise><xsl:text>, </xsl:text></xsl:otherwise>
</xsl:choose>
</xsl:if>
<xsl:value-of select="substring(first-name, 1, 1)"></xsl:value-of>
<xsl:text>. </xsl:text>
<xsl:value-of select="last-name"/>
</xsl:for-each>
</xsl:otherwise>
</xsl:choose>
<xsl:text>,
    </xsl:text>
<xsl:text>\textquotedblleft{</xsl:text>
<xsl:value-of select="contribution/title"/>
<xsl:text>}\textquotedblright,
    </xsl:text>

<xsl:choose>
<xsl:when test="contribution/status = 'in_proceedings'">
<xsl:text>in emph{Proc. </xsl:text>
<xsl:value-of select="conference/booktitle-short"/>
<xsl:text>}</xsl:text>
</xsl:when>
<xsl:otherwise>
<xsl:text>presented at </xsl:text>
<xsl:value-of select="conference/booktitle-short"/>
</xsl:otherwise>
</xsl:choose>

<xsl:text>, </xsl:text>
<xsl:value-of select="conference/venue"/>

<xsl:variable name="dateString" select="conference/start-date"/>

<xsl:if test="$dateString != ''">
<xsl:text>, </xsl:text>
<xsl:value-of select="conference/month"/>
<xsl:variable name="monthShort" select="conference/month"/>
<xsl:if test="$monthShort != 'May'"><xsl:text>.</xsl:text></xsl:if>
<xsl:text> </xsl:text>
<xsl:value-of select="conference/year"/>
</xsl:if>

<xsl:choose>
    <xsl:when test="contribution/pages/number > 0">
        <xsl:text>, pp. </xsl:text>
        <xsl:value-of select="contribution/pages/start-page"/>
        <xsl:text>-</xsl:text>
        <xsl:variable name="endPage" select="contribution/pages/start-page + contribution/pages/number - 1"/>
        <xsl:value-of select="$endPage"/>
        <xsl:text>.</xsl:text>
        <xsl:if test="contribution/doi != ''">
            <xsl:text>
    \url{doi:</xsl:text>
            <xsl:value-of select="contribution/doi"/>
            <xsl:text>}
            </xsl:text>
        </xsl:if>
    </xsl:when>
    <xsl:when test="contribution/status = 'conference'">
        <xsl:text>, this conference.</xsl:text>
    </xsl:when>
    <xsl:otherwise>
        <xsl:text>, paper </xsl:text>
        <xsl:value-of select="contribution/paper-code"></xsl:value-of>
        <xsl:text>, unpublished.</xsl:text>
    </xsl:otherwise>
</xsl:choose>

</xsl:template>

</xsl:stylesheet>